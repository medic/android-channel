apply plugin: 'com.android.application'


android {
    compileSdkVersion 28
    buildToolsVersion '29.0.2'
    lintOptions {
        abortOnError true
    }


    signingConfigs {
        release {
            storeFile file(System.env.ANDROID_KEYSTORE_PATH ?: signingConfigs.debug.storeFile)
            storePassword System.env.ANDROID_KEYSTORE_PASSWORD ?: signingConfigs.debug.storePassword
            keyAlias System.env.ANDROID_KEY_ALIAS ?: signingConfigs.debug.keyAlias
            keyPassword System.env.ANDROID_KEY_PASSWORD ?: signingConfigs.debug.keyPassword
        }
    }


    defaultConfig {
        if (project.hasProperty('packNumber')) {
            applicationId "io.rapidpro.androidchannel.pack$packNumber"
        } else {
            applicationId "io.rapidpro.androidchannel.pack1"
        }

        minSdkVersion 14
        targetSdkVersion 26
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    lintOptions {
        lintConfig = new File('config/lint.xml')

        disable 'UnusedResources' // linter can't handle static imports, so just skip this test
        disable 'GradleDependency' // TODO update to latest support-v4 lib and re-enable this rule
        disable 'OldTargetApi'

        warningsAsErrors true

        xmlReport false

        if(System.env.TRAVIS == 'true') {
            abortOnError true
            htmlReport false
            textReport true
            textOutput 'stdout'
        }
    }
}